plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.4'
    id 'net.researchgate.release' version '2.5.0'
    id 'co.riiid.gradle' version '0.4.2'
    id 'java'
    id 'maven'
    id 'idea'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
}

shadowJar {
    relocate 'org.apache.mina', 'shadow_2_0_9.org.apache.mina'
    classifier = null
}

jar.enabled = false

dependencies {
    compileOnly group: 'com.github.azkaban', name: 'azkaban', version: '3.73.1'
    compile group: 'org.apache.directory.api', name: 'api-all', version: '1.0.3'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.zapodot', name: 'embedded-ldap-junit', version: '0.5.2'
    testCompile group: 'com.github.azkaban', name: 'azkaban', version: '3.73.1'
}

github {
    owner = 'researchgate'
    repo = 'azkaban-ldap-usermanager'
    token = project.hasProperty('githubToken') ? githubToken : 'Set token'
    tagName = project.version
    targetCommitish = 'master'
    name = 'v' + project.version
    assets = [
        shadowJar.getArchivePath()
    ]
}

artifacts {
    shadowJar;
}

githubRelease {
    doFirst {
        if (!project.hasProperty('githubToken')) {
            throw new Exception("No github token is set!")
        }
    }
}

release {
    git {
        requireBranch = '(master|\\d+\\.\\d+)'
    }
}

build.dependsOn(shadowJar);

release.buildTasks = ["build"];
afterReleaseBuild.dependsOn githubRelease
createReleaseTag.enabled = false
